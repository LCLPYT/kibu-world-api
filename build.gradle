import work.lclpnet.build.task.GithubDeploymentTask

plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'maven-publish'
	id 'gradle-build-utils' version '1.7.0'
}

Properties props = buildUtils.loadProperties('publish.properties')  // will be empty, if the file is missing

version = buildUtils.gitVersion()  // determine project version from the latest git tag on the current branch
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url "https://repo.lclpnet.work/repository/internal"
    }
    maven {
        url "https://repo.lclpnet.work/repository/snapshots"
    }
    maven {
        url = 'https://maven.nucleoid.xyz/'
    }
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath + main.getOutput()
        runtimeClasspath += main.runtimeClasspath + main.getOutput()
    }
}

loom {
    runs {
        named('server') {
            source 'testmod'
        }

        named('client') {
            source 'testmod'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "xyz.nucleoid:fantasy:${rootProject.fantasy_version}"
}

[processResources, processTestmodResources].each {
	it.inputs.property "version", project.version
    it.inputs.property "fantasy_version", project.fantasy_version

	it.filesMatching("fabric.mod.json") {
		expand "version": project.version, "fantasy_version": project.fantasy_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(21)
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

[jar, remapJar, sourcesJar].each {
	it.from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

def env = System.getenv()

tasks.register("github", GithubDeploymentTask) {
    dependsOn tasks.remapJar

    config {
        token = env.get("GITHUB_TOKEN")
        repository = env.get("GITHUB_REPOSITORY")
    }

    release {
        title = "[$project.minecraft_version] $project.name $project.version"
        tag = buildUtils.latestTag()
    }

    assets.add(tasks.remapJar.archiveFile.get())
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()

            from components.java

            pom {
                name = 'kibu-world-api'
                description = 'API for loading / unloading worlds with the fantasy mod'
            }
        }
    }

    // automatically use DEPLOY_URL, DEPLOY_USER and DEPLOY_PASSWORD environment variables
    // or mavenHost, mavenUser and mavenPassword from props
    buildUtils.setupPublishRepository(repositories, props)
}